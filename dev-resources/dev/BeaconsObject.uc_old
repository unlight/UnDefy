class BeaconsObject extends Object;
// тип локации: 1 - near pickup 2 - between p1 & p2
// лифты 1. около LiftCenter есть всегда LiftExit
// todo: мутатор MutNoSuperWeapon заменяет супер оружие
// else if ( xWeaponBase(Other).WeaponType == class'Redeemer' ) xWeaponBase(Other).WeaponType = class'RocketLauncher';
// todo: добавить ближайший пикап 2 и если разница между ними 10 м и он в прямой видимости то используем его

var array<string> TempBeaconItemNames;
var array<string> BeaconItemNames;
var array<vector> BeaconCoordinates;
var Actor SomeActor;

function Initialize(Actor Dummy){
	SomeActor = Dummy;
	ProcessPickups();
	ProcessNavigationPoints();
	ProcessBeacons();
}

function ProcessBeacons(){
	local byte i, j;
	local string NearestItem, Letter, ItemLetter;
	for(i = 0; i < BeaconItemNames.Length; i++){
		NearestItem = NearestBeacon(BeaconCoordinates[i]);
		if(NearestItem == "") continue;
		ItemLetter = Right(BeaconItemNames[i], 2);
		for(j = 65; j < 90; j++){
			Letter = " " $ Chr(j);
			if(ItemLetter != Letter) continue;
			BeaconItemNames[i] @= NearestItem;
			break;
		}
	}
}

function string NearestBeacon(vector V){
	local float MinDistance, Distance;
	local byte i;
	local string S;
	MinDistance = 52.5 * 15; // + 5-10 m для lineofsight pickupa
	for(i = 0; i < TempBeaconItemNames.Length; i++){
		if(V == BeaconCoordinates[i]) continue;
		Distance = VSize(V - BeaconCoordinates[i]);
		if(Distance > MinDistance) continue;
		MinDistance = Distance;
		S = TempBeaconItemNames[i];
	}
	if(S != "") S = Repl("(%P)", "%P", S);
	return S;
}

function ProcessNavigationPoints(){
	local NavigationPoint N;
	local string S;
	for(N = SomeActor.Level.NavigationPointList; N != None; N = N.NextNavigationPoint){
		if(N.IsA('Teleporter')) S = "Teleport";
		else if(N.IsA('Door')) S = "Door";
		else if(N.IsA('LiftCenter')) S = "Lift";
		else if(N.IsA('JumpPad')) S = "Jump Pad";
		else S = "";
		if(S != "") RegisterBeacon(N.Location, S);
	}
}

function ProcessPickups(){
	local Pickup P;
	local string S;
	foreach SomeActor.DynamicActors(class'Pickup', P){
		if(P.IsA('WeaponPickup')) S = P.GetHumanReadableName();
		else if(P.IsA('UDamagePack')) S = "DOUBLE DAMAGE";
		else if(P.IsA('ShieldPickup')) S = ShieldPickup(P).ShieldAmount @ "Shield";
		else if(P.IsA('SuperHealthPack')) S = "Super Health";
		else continue;
		RegisterBeacon(P.Location, S);
	}
}

function RegisterBeacon(vector Coordinate, string S){
	local byte i, j, Count;
	local array<byte> Keys;
	i = TempBeaconItemNames.Length;
	BeaconCoordinates[i] = Coordinate;
	TempBeaconItemNames[i] = S;
	Count = CountStrings(S, TempBeaconItemNames, Keys);
	if(Count == 1){ BeaconItemNames[i] = TempBeaconItemNames[i]; return;	}
	for(j = 0; j < Keys.Length; j++){
		BeaconItemNames[Keys[j]] = TempBeaconItemNames[Keys[j]] @ Chr(65 + j);
	}
}

function byte CountStrings(string S, array<string> A, optional out array<byte> Keys){
	local byte i, Count;
	Keys.Length = 0;
	for(i = 0; i < A.Length; i++){
		if(S != A[i]) continue;
		Keys[Keys.Length] = i;
		Count++;
	}
	return Count;
}

simulated function Debug(coerce string S){
	class'zUtil'.static.Debug(S, SomeActor);
}
defaultproperties{}
